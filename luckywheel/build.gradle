plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    id("maven-publish")
}

android {
    namespace 'com.caneryilmaz.apps.luckywheel'
    compileSdk 34

    defaultConfig {
        minSdk 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}

afterEvaluate {
    publishing {
        publications {
            create("release", MavenPublication) {
                from components.release
                groupId = "com.github.caneryilmaz52"
                artifactId = "LuckyWheelView"
                version = "1.0"

                pom {
                    name.set("Lucky Wheel View")
                    description.set("A custom Lucky Wheel View developed with Kotlin.")
                    url.set("https://github.com/caneryilmaz52/LuckyWheelView")

                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    developers {
                        developer {
                            id.set("caneryilmaz52")
                            name.set("Caner YILMAZ")
                            email.set("caneryilmaz.apps@gmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:github.com/caneryilmaz52/LuckyWheelView.git")
                        developerConnection.set("scm:git:ssh://github.com/caneryilmaz52/LuckyWheelView.git")
                        url.set("https://github.com/caneryilmaz52/LuckyWheelView")
                    }
                }
            }
        }
        repositories {
            maven {
                url = uri("$buildDir/outputs/repo")
            }
        }
    }
}